/**
 * @description 
 * @author 
 */
rule Rules.FraudDetection {
	
	attribute {
		priority = 5;
		forwardChain = true;
	}
	
	declare {
		Concepts.Account	account;	
	}
	
	when {
		//1. Checks the number of debits in the verification interval
		account.DebitCount >= FraudCriteria.numTransactions;
		
		//2. Atleast 1 debit should have been done
		account.DebitUpdate != null;
		
		//3. If the number of debits have happened within the time interval (2 min)
		(DateTime.getTimeInMillis(DateTime.now()) - DateTime.getTimeInMillis(account.DebitUpdate)) <= FraudCriteria.interval;
		
		//3. Checks that the Account status is not set to Suspended
		account.Status != "Suspended";
	}
	
	then {
		if (account.FraudAttempt >= FraudCriteria.fraudAttempts) {
			account.Status = "Suspended";
			account.Reason = "Fraud Detected";
			System.debugOut("#### Account ID " + account@extId + " STATUS set to Suspended. Fraud detected.");
		} else {
			account.FraudAttempt++;
			System.debugOut("#### Account ID " + account@extId + " Fraud suspected, Attempt [" + account.FraudAttempt + "]");
		}
		account.Updated = DateTime.now();
		
		// Create Fraud Metric
		DateTime now = DateTime.now();
		Instance.createInstance("xslt://{{/Concepts/FraudMetric}}<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n<xsl:stylesheet xmlns:xsl=\"http://www.w3.org/1999/XSL/Transform\" version=\"2.0\"><xsl:param name=\"account\"/><xsl:param name=\"now\"/><xsl:template name=\"Function\" match=\"/\"><createObject><object><FraudDate><xsl:value-of select=\"$now\"/></FraudDate><xsl:if test=\"$account/@extId\"><AccountId><xsl:value-of select=\"$account/@extId\"/></AccountId></xsl:if></object></createObject></xsl:template></xsl:stylesheet>");
		
		Event.assertEvent(Event.createEvent("xslt://{{/Events/AccountOperationTrail}}<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n<xsl:stylesheet xmlns:xsl=\"http://www.w3.org/1999/XSL/Transform\" version=\"2.0\"><xsl:param name=\"account\"/><xsl:param name=\"now\"/><xsl:template name=\"Function\" match=\"/\"><createEvent><event><xsl:if test=\"$account/@extId\"><AccountId><xsl:value-of select=\"$account/@extId\"/></AccountId></xsl:if><OpDate><xsl:value-of select=\"$now\"/></OpDate><OpType><xsl:value-of select=\"&quot;FRAUD&quot;\"/></OpType></event></createEvent></xsl:template></xsl:stylesheet>"));
	}
}