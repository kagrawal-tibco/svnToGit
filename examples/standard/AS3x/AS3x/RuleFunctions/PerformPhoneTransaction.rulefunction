/**
 * @description 
 */
void rulefunction RuleFunctions.PerformPhoneTransaction {
	attribute {
		validity = ACTION;
	}
	scope {
		Events.PerformPhoneTransaction phoneTransaction;
	}
	body {
		String asURL = "http://localhost:5055";
		String containerName = "Phone";
		
		// 0. Enable Transactions
		// 1. Put 2 phone concepts
		// 2. Delete one from it
		// 3. Query Phones - Nothing from #1 & #2 should be visible
		// 4. Commit
		// 5. Query Phones again - Should show #1 + #2 (only 1 new phone entry)
		
		System.debugOut("Enable Transactions...");
		Store.Transactions.enableTransactions(asURL);
		
		Store.open(asURL, containerName);
		
		System.debugOut("Adding Phone 1...");
		
		// Put Phone 1
		Object phItem = Store.Item.create(asURL, containerName);
		
		long phoneId = System.nanoTime();
		Store.Item.setLong(phItem, "phoneId", phoneId);
		Store.Item.setString(phItem, "home", "111-111-1111");
		Store.Item.setString(phItem, "mobile", "222-222-2222");
		
		Store.put(asURL, containerName, phItem);
		
		Store.Item.clear(phItem);
		
		// Put Phone 2
		System.debugOut("Adding Phone 2...");
		
		Store.Item.setLong(phItem, "phoneId", System.nanoTime());
		Store.Item.setString(phItem, "home", "333-333-3333");
		Store.Item.setString(phItem, "mobile", "444-444-4444");
		
		Store.put(asURL, containerName, phItem);
		
		Store.Item.clear(phItem);
		
		// Get Phone 1
		Store.Item.setLong(phItem, "phoneId", phoneId);
		
		Object returnedPhone = Store.get(asURL, containerName, phItem);
		
		Object stringBuff = String.createBuffer(10);
		String.append(stringBuff,"Phone details,\n");
		String.append(stringBuff,"\tPhoneId - " + Store.Item.getLong(returnedPhone, "phoneId") + "\n");
		String.append(stringBuff,"\tHome - " + Store.Item.getString(returnedPhone, "home") + "\n"); 
		String.append(stringBuff,"\tMobile - " + Store.Item.getString(returnedPhone, "mobile") + "\n");

		System.debugOut("Getting Phone 1, Details .... \n" + String.convertBufferToString(stringBuff) + "\n");
		
		Store.Item.destroy(returnedPhone);
		
		// Delete Phone 1
		System.debugOut("Deleting Phone 1...");
		Store.Item.clear(phItem);
		Store.Item.setLong(phItem, "phoneId", phoneId);
		
		Store.delete(asURL, containerName, phItem);
		
		Store.Item.destroy(phItem);
		
		Store.close(asURL, containerName);
		
		// Query Phones
		QueryPhones();
		
		System.debugOut("Commit Transaction...");
		Store.Transactions.commit(asURL);
		System.debugOut("Disable Transactions...");
		Store.Transactions.disableTransactions(asURL);
		
		// Query Employees
		QueryPhones();
		
		Event.replyEvent(phoneTransaction, phoneTransaction);
	}
}