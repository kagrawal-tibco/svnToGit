/**
 * @description 
 */
void rulefunction Monitoring.RuleFunctions.cqueries.BestOrWorstThreadOrJobPoolQueryListener {
	attribute {
		validity = ACTION;
	}
	scope {
		String	listenerName;
		boolean	isBatchEnd;
		boolean	hasEnded;
		Object	row;
		Object	closure;
	}
	body {
		if (isBatchEnd) {
		    String configMapId = closure;
		    String cType = Util.HashMap.getObject(configMapId,"ctype");
		    String[] mergedListIDs = processBatch(configMapId);
		    for (int i = 0 ; i < mergedListIDs@length ; i = i + 1){
		        String mergedListID = mergedListIDs[i];
		        //mergedListID is of the form ctype::hostname::merged
		        String hostName = String.split(mergedListID,"::")[1];
		        int finalCnt = BEMMUtil.LinkedList.size(mergedListID);
		        
		        Shared.RuleFunctions.log("SymantecDebug","DEBUG","["+cType+"]::Found "+mergedListID+" with "+finalCnt+" elements and host name as "+hostName);
		        
		        Shared.RuleFunctions.log("BestOrWorstThreadOrJobPoolQueryListener","DEBUG","["+cType+"]::Found "+mergedListID+" with "+finalCnt+" elements and host name as "+hostName);
		        //fire off the events
		        Monitoring.Events.bemetrics.AbstractIndexableBEMetricEvent event = null;
		        String categories = null;
		        String threadList = null;
		        String queueList = null;
		        String name = null;
		        for (int j = 0 ; j < finalCnt ; j = j + 1){
		            Object[] data = BEMMUtil.LinkedList.get(mergedListID,j);
		            if(data == null)continue;
		            //boolean batchStart = (j == 0);
		            //boolean batchEnd = (j + 1 == finalCnt);
		            name = data[1] + ":"+ cType;
		            
		            if (categories == null) {
		            	categories = data[1];
		            }
		            else {
			            categories = categories + ";" + data[1];
		            }
		            if (String.indexOfString(cType,0,"tpool") != -1) {
		                int maxThreads = data[2];
		                int activeThreads = data[3];
		                if(threadList == null)
		                threadList = String.valueOfInt(activeThreads);
		                else
		                threadList = threadList +";"+ String.valueOfInt(activeThreads);
		               
		            }
		            else if (String.indexOfString(cType,0,"jqueue") != -1) {
		                int queueCapacity = data[2];
		                int queueSize = data[3];
		                if(queueList == null)
		                queueList = String.valueOfInt(queueSize);
		                else
		                queueList = queueList +";"+ String.valueOfInt(queueSize);		                		
		            }	            
		        }
		        
		        event = Monitoring.Events.bemetrics.ThreadAndJobPoolGroupEvent.ThreadAndJobPoolGroupEvent(
		                            null,null,0,true,true,finalCnt,"",hostName,name,categories, threadList,queueList);
		        Shared.RuleFunctions.log("BestOrWorstThreadOrJobPoolQueryListener","DEBUG","["+cType+"]::Firing AbstractIndexableBEMetricEvent[id="+event@id+",hostName="+event.hostName+",metricName="+event.metricName+"]"+categories+"---"+threadList+"---"+queueList);                    
		        Event.sendEvent(event);
		        BEMMUtil.LinkedList.deleteList(mergedListID);
		    }
		    return;
		} else if (hasEnded) {
		    return;
		} else {
		    String configMapId = closure;
		    String cType = Util.HashMap.getObject(configMapId,"ctype");
		    //will create the master cType host list if needed
		    BEMMUtil.LinkedList.createList(cType);
		    Object[] columns = row;
		    if(columns == null) return;
		    //get host fqname
		    String host = columns[0];
		    //host are maintained per cType
		    String hostWithCTypeId = cType + "::" + host;
		    if (BEMMUtil.LinkedList.contains(cType,hostWithCTypeId) == false){
		        //create host-data list
		        BEMMUtil.LinkedList.createList(hostWithCTypeId);
		        //add the new host list to the master cType host list
		        BEMMUtil.LinkedList.add(cType,BEMMUtil.LinkedList.size(cType),hostWithCTypeId);
		    }
		    int newIdx = BEMMUtil.LinkedList.size(hostWithCTypeId);
		    //get metric name
		    String name = columns[1] + ":"+ cType;
		   Shared.RuleFunctions.log("BestOrWorstThreadOrJobPoolQueryListener","DEBUG","["+cType+"]::["+hostWithCTypeId+"]::Adding ["+BEMM.util.join(columns,",")+"]");
		   BEMMUtil.LinkedList.add(hostWithCTypeId,newIdx,columns);
		}
	}
}